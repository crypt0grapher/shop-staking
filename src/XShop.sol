// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

//import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "lib/openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import "lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";
import "lib/openzeppelin-contracts/contracts/access/Ownable.sol";
import "lib/openzeppelin-contracts/contracts/security/ReentrancyGuard.sol";
import "lib/v2-periphery/contracts/interfaces/IUniswapV2Router02.sol";
/// @title A shop staking contract
/// @author crypt0grapher
/// @notice this contract stakes SHOP tokens and allows users to earn rewards from the fees generated by the platform

contract XShop is ERC20, Ownable, ReentrancyGuard {
    event Snapshot(uint256 rewards, address indexed from, uint256 shopMinted);
    event Deposited(address indexed user, uint256 amount);
    event Withdrawn(address indexed user, uint256 amount);
    event Claimed(address indexed user, uint256 reward);
    event ReinvestingToggled(address indexed user, bool status);

    IERC20 public constant shopToken = IERC20(0x99e186E8671DB8B10d45B7A1C430952a9FBE0D40);
    IUniswapV2Router02 public constant uniswapRouter = IUniswapV2Router02(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);

    uint256 public minimumStake = 20000 * 10e18;
    // Staking time lock, 5 days by default
    uint256 public timeLock = 5 days;

    uint256 public epochDuration = 1 days;
    // Total rewards injected
    uint256 public totalRewards;

    // Snapshot of the epoch, generated by the snapshot(), used to calculate rewards
    struct EpochInfo {
        // Snapshot time
        uint256 timestamp;
        // Rewards injected
        uint256 rewards;
        // Total deposited snap
        uint256 supply;
        // $SHOP swapped for rewards for re-investors
        uint256 shop;
    }

    uint256 public currentEpoch;
    mapping(uint256 => EpochInfo) public epochInfo;

    // User info, there's also a balance of xSHOP on the ERC20 super contract
    struct UserInfo {
        //epoch => total amount deposited during the epoch
        mapping(uint256 => uint256) depositedInEpoch;
        mapping(uint256 => bool) isReinvestingOnForEpoch;
        // a starting epoch for reward calculation for user - either last claimed or first deposit
        uint256 lastClaimedEpoch;
        uint256 lastEpochReinvested;
    }

    mapping(address => UserInfo) public userInfo;

    // That's for enumerating re-investors because we have to iterate over them to buy SHOP for rewards generated
    uint256 public reInvestorsCount;
    mapping(address => uint256) public reInvestorsIndex;
    mapping(uint256 => address) public reInvestors;

    // ========== Configuration ==========
    constructor() ReentrancyGuard() Ownable() ERC20("Staked SHOP", "xSHOP") {
    }

    function setMinimumStake(uint256 _minimumStake) public onlyOwner {
        minimumStake = _minimumStake;
    }

    function setTimeLock(uint256 _timeLock) public onlyOwner {
        timeLock = _timeLock;
    }

    // ========== State changing ==========

    function deposit(uint256 amount) public nonReentrant {
        require(amount + balanceOf(msg.sender) > minimumStake, "Amount must be greater than 20K $SHOP");
        require(shopToken.transferFrom(msg.sender, address(this), amount), "Transfer failed");

        _updateStake(msg.sender, amount, true);

        emit Deposited(msg.sender, amount);
    }

    function withdraw(uint256 amount) public nonReentrant {
        require(balanceOf(msg.sender) >= amount, "Insufficient balance");

        _updateStake(msg.sender, amount, false);

        require(shopToken.transfer(msg.sender, amount), "Transfer failed");

        emit Withdrawn(msg.sender, amount);
    }

    function claimReward() public nonReentrant {
        require(currentEpoch > 0, "No rewards have been distributed yet");
        uint256 lastSnapshotTime = epochInfo[currentEpoch - 1].timestamp;
        require(lastSnapshotTime + epochDuration < block.timestamp, "Too early to calculate rewards");
        require(!isReinvesting(), "Auto-compounding is enabled");

        uint256 reward = getPendingReward();
        require(reward > 0, "No reward available");

        userInfo[msg.sender].lastClaimedEpoch = currentEpoch;
        payable(msg.sender).transfer(reward);

        emit Claimed(msg.sender, reward);
    }

    function snapshot() public payable nonReentrant {
        uint256 lastSnapshotTime = epochInfo[currentEpoch - 1].timestamp;
        require(block.timestamp >= lastSnapshotTime + epochDuration - 5 minutes, "Too early for a new snapshot");
        require(msg.value > 0, "ETH amount must be greater than 0");

        epochInfo[currentEpoch].rewards = msg.value;
        epochInfo[currentEpoch].timestamp = block.timestamp;
        epochInfo[currentEpoch].supply = totalSupply();
        // swap ETH for autocompounding

        uint256 ethToSell = 0;
        for (uint256 i = 1; i <= reInvestorsCount; i++) {
            ethToSell += _calculateRewardToReinvest(reInvestors[i]);
            if (ethToSell > 0) {
                userInfo[reInvestors[i]].lastEpochReinvested = currentEpoch;
            }
        }
        uint256 xShopToMintTotal = _swapEthForShop(ethToSell);
        epochInfo[currentEpoch].shop = xShopToMintTotal;

        currentEpoch++;
        emit Snapshot(msg.value, msg.sender, xShopToMintTotal);
    }


    function toggleReinvesting() public {
        bool currentStatus = reInvestorsIndex[msg.sender] > 0;
        if (!currentStatus) {
            // Add re-investor to the renumeration
            if (reInvestorsIndex[msg.sender] == 0) {
                reInvestorsCount++;
                reInvestorsIndex[msg.sender] = reInvestorsCount;
                reInvestors[reInvestorsCount] = msg.sender;
                userInfo[msg.sender].isReinvestingOnForEpoch[currentEpoch] = true;
            }
        }
        else {
            // Remove re-investor from the renumeration
            if (reInvestorsIndex[msg.sender] != 0) {
                uint256 index = reInvestorsIndex[msg.sender];
                address lastReinvestor = reInvestors[reInvestorsCount];

                // Swap the msg.sender to remove with the last msg.sender
                reInvestors[index] = lastReinvestor;
                reInvestorsIndex[lastReinvestor] = index;

                // Remove the last msg.sender and update count
                delete reInvestors[reInvestorsCount];
                delete reInvestorsIndex[msg.sender];
                reInvestorsCount--;
                userInfo[msg.sender].isReinvestingOnForEpoch[currentEpoch] = false;
            }
        }
        emit ReinvestingToggled(msg.sender, !currentStatus);
    }


    function rescueETH(uint256 weiAmount) external {
        payable(owner()).transfer(weiAmount);
    }

    function rescueERC20(address tokenAdd, uint256 amount) external {
        IERC20(tokenAdd).transfer(owner(), amount);
    }

    // ========== View functions ==========

    function getPendingReward() public view returns (uint256) {
        return calculateRewardForUser(msg.sender);
    }

    function calculateRewardForUser(address user) public view returns (uint256) {
        uint256 reward = 0;
        if (currentEpoch == 0) {
            return 0;
        }
        uint256 userBalanceInEpoch = balanceOf(user);
        for (uint256 i = currentEpoch - 1; i >= userInfo[user].lastClaimedEpoch; i--) {
            userBalanceInEpoch -= userInfo[user].depositedInEpoch[i];
            if (reInvestorsIndex[msg.sender] == 0) {
                uint256 epochReward = userBalanceInEpoch * epochInfo[i].rewards / epochInfo[i].supply;
                reward += epochReward;
            }
        }
        return reward;
    }

    function isReinvesting() public view returns (bool) {
        return reInvestorsIndex[msg.sender] > 0;
    }

    // ========== Internal functions ==========

    function _updateStake(address user, uint256 amount, bool isDeposit) internal {
        if (isDeposit) {
            userInfo[user].depositedInEpoch[currentEpoch] += amount;
            _mint(user, amount);
        } else {
            uint256 remainingAmountToWithdraw = amount;
            for (uint256 i = 0; i <= currentEpoch; i++) {
                if (remainingAmountToWithdraw == 0) {
                    break;
                }
                uint256 amountToWithdraw = userInfo[user].depositedInEpoch[i];
                if (amountToWithdraw > remainingAmountToWithdraw) {
                    amountToWithdraw = remainingAmountToWithdraw;
                }
                userInfo[user].depositedInEpoch[i] -= amountToWithdraw;
                remainingAmountToWithdraw -= amountToWithdraw;
            }
            _burn(user, amount);
        }
    }


    function _swapEthForShop(uint256 ethAmount) private returns (uint256) {
        // Generate the Uniswap pair path of WETH -> SHOP
        address[] memory path = new address[](2);
        path[0] = uniswapRouter.WETH();
        path[1] = address(shopToken);

        uint256 deadline = block.timestamp + 15; // 15 seconds from the current block time

        // Swap and return the amount of SHOP tokens received
        uint[] memory amounts = uniswapRouter.swapExactETHForTokens{value: ethAmount}(
            0, // Accept any amount of SHOP
            path,
            address(this),
            deadline
        );

        // Return the amount of SHOP tokens received
        return amounts[1];
    }

// Mock function for demonstration purposes. In reality, you'd interact with a decentralized exchange contract here.
    function _calculateRewardToReinvest(address user) private view returns (uint256) {
        uint256 reward = 0;
        if (currentEpoch == 0) {
            return 0;
        }
        uint256 userBalanceInEpoch = balanceOf(user);
        for (uint256 i = currentEpoch - 1; i >= userInfo[user].lastEpochReinvested; i--) {
            userBalanceInEpoch -= userInfo[user].depositedInEpoch[i];
            if (reInvestorsIndex[msg.sender] > 0) {
                uint256 rewardsToReinvest = userBalanceInEpoch * epochInfo[i].rewards / epochInfo[i].supply;
                reward += rewardsToReinvest;
            }
        }
        return reward;
    }

    function _beforeTokenTransfer(address from, address to, uint256 amount) internal override {
        super._beforeTokenTransfer(from, to, amount);
        require(from == address(0) || to == address(0), "Only stake or unstake");
    }


}
